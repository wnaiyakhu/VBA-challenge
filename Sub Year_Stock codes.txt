Sub Year_Stock()
    ' Declare variables
    Dim ws As Worksheet
    Dim Ticker As String
    Dim LastRow As Long
    Dim i As Long
    Dim j As Long
    Dim Open_Price As Double
    Dim Close_Price As Double
    Dim Ticker_vol As Double
    Dim Daily_Change As Double
    Dim Total As Double
    Dim MaxIncrease As Double
    Dim MaxDecrease As Double
    Dim MaxVolume As Double
    Dim MaxIncreaseTicker As String
    Dim MaxDecreaseTicker As String
    Dim MaxVolumeTicker As String
    ' Loop worksheets
    For Each ws In ThisWorkbook.Worksheets
        ' Set initial values
        j = 2
        Total = 0
        Daily_Change = 0
        Open_Price = ws.Cells(2, 3).Value
        MaxIncrease = 0
        MaxDecrease = 0
        MaxVolume = 0
        MaxIncreaseTicker = ""
        MaxDecreaseTicker = ""
        MaxVolumeTicker = ""
        ' Create column names
        ws.Range("I1").Value = "Ticker"
        ws.Range("J1").Value = "Yearly Change"
        ws.Range("K1").Value = "Percent Change"
        ws.Range("L1").Value = "Total Stock Volume"
        ' Determine the last row
        LastRow = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
        ' Loop data
        For i = 2 To LastRow
            Ticker_vol = ws.Cells(i, 7).Value
            Total = Total + Ticker_vol
            If ws.Cells(i + 1, 1).Value <> ws.Cells(i, 1).Value Then
                Ticker = ws.Cells(i, 1).Value
                If Total = 0 Then
                    ws.Range("I" & j).Value = Ticker
                    ws.Range("J" & j).Value = 0
                    ws.Range("K" & j).Value = Format(0, "Percent")
                    ws.Range("L" & j).Value = 0
                Else
                    ' Calculate the values
                    Close_Price = ws.Cells(i, 6).Value
                    Daily_Change = Close_Price - Open_Price
                    ' Write the calculated values
                    ws.Cells(j, 9).Value = Ticker
                    ws.Cells(j, 10).Value = Daily_Change
                    ws.Cells(j, 11).Value = Format(IIf(Open_Price <> 0, Daily_Change / Open_Price, 0), "Percent")
                    ws.Cells(j, 12).Value = Total
                End If
                If Daily_Change > MaxIncrease Then
                    MaxIncrease = Daily_Change
                    MaxIncreaseTicker = Ticker
                End If
                If Daily_Change < MaxDecrease Then
                    MaxDecrease = Daily_Change
                    MaxDecreaseTicker = Ticker
                End If
                If Total > MaxVolume Then
                    MaxVolume = Total
                    MaxVolumeTicker = Ticker
                End If
                ' Prepare for next stock
                Open_Price = ws.Cells(i + 1, 3).Value
                j = j + 1
                Total = 0
            End If
        Next i
        ws.Range("P2").Value = MaxIncreaseTicker
        ws.Range("P3").Value = MaxDecreaseTicker
        ws.Range("P4").Value = MaxVolumeTicker
        ws.Range("Q2").Value = Format(MaxIncrease, "Currency")
        ws.Range("Q3").Value = Format(MaxDecrease, "Currency")
        ws.Range("Q4").Value = MaxVolume
    Next ws
End Sub

